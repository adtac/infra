# Maintainer: Adhityaa Chandrasekar <archlinux AT adtac DOT in>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Army <uli[dot]armbruster[at]gmail[dot]com>

pkgname=ncmpcpp
pkgver=0.8.2
pkgrel=8
pkgdesc='Almost exact clone of ncmpc with some new features'
url='http://ncmpcpp.rybczak.net/'
arch=('x86_64')
license=('GPL')
depends=('curl' 'libmpdclient' 'taglib' 'ncurses' 'fftw' 'boost-libs')
makedepends=('boost')
conflicts=('ncmpcpp')
source=(${pkgname}-${pkgver}.tar.bz2::https://rybczak.net/ncmpcpp/stable/${pkgname}-${pkgver}.tar.bz2
        no-separator.patch)
sha512sums=('9384edd162f40af23e4f26f437356520f117e4e2ab513ed3de132d2f11e597082d1cb7f0dd3696660d0c9bb85ad03e31fb921c97a11c7b28b32c5907fdbb8e8f'
            '6574bf6ed020fe97f114f95267abacf77bc56bc12a0f12187ea4b53324290f64356a90bf97d4a24dc2c2a79e12bcdb9c269dbce3fe93b92bfce6546593eef5e5')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i "$srcdir"/no-separator.patch
}

build() {
  cd ${pkgname}-${pkgver}

  # http://site.icu-project.org/download/61#TOC-Migration-Issues
  CPPFLAGS+=' -DU_USING_ICU_NAMESPACE=1'

  BOOST_LIB_SUFFIX='' ./configure \
    --prefix=/usr \
    --enable-clock \
    --enable-outputs \
    --enable-unicode \
    --enable-visualizer \
    --with-curl \
    --with-fftw \
    --with-taglib
  make
  make -C extras
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 755 extras/artist_to_albumartist -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
